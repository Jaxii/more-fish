import org.apache.tools.ant.filters.*

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "com.github.johnrengelman.shadow"

group = 'me.elsiff'
version = '3.0.4'

ext.pluginName = 'MoreFish'
ext.mainPackage = 'me.elsiff.morefish'
ext.mainClass = "${mainPackage}.MoreFish"
ext.spigotVersion = '1.15.2-R0.1-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.3"
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "http://nexus.hc.to/content/repositories/pub_releases" }
    maven { url "http://repo.citizensnpcs.co/" }
    maven { url "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url "https://repo.aikar.co/content/groups/aikar/" }
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" }
    maven { url 'https://jitpack.io' }
    mavenLocal()
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    implementation 'co.aikar:acf-core:0.5.0-SNAPSHOT'
    implementation 'co.aikar:acf-paper:0.5.0-SNAPSHOT'
    compileOnly 'com.comphenix.protocol:ProtocolLib:4.5.0'
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.2'
    compileOnly "org.spigotmc:spigot-api:${spigotVersion}"
    compileOnly "org.spigotmc:spigot:${spigotVersion}"
    compileOnly 'net.milkbowl.vault:VaultAPI:1.7'
    compileOnly 'net.citizensnpcs:citizensapi:2.0.26-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.10.3'
    compileOnly 'com.github.mcMMO-Dev:mcMMO:f82ad99c82'
}

shadowJar {
    relocate 'co.aikar.commands', "${mainPackage}.acf"
}
build { dependsOn shadowJar }

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "pluginName"     : pluginName,
            "project.version": project.property("version"),
            "authors"         : "elsiff, Musician101",
            "websites"        : "https://elsiff.me, musician101.github.io",
            "mainClass"      : mainClass
    ]
}
